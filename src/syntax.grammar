@precedence {
  right @right
}

//// Templates

list<x> { (x ',')* x? }

//// Root

@top Nasl { (instr | FuncDecl)* }

//// Function Declaration

FuncDecl { kw<'function'> Identifier ArgDecl Block }

ArgDecl { '(' list<Identifier> ')' }

//// Block

Block { '{' instr* '}' }

//// Instruction

instr {
  SimpleInstr ';'
  | Block
  | IfBlock
  | loop
}

//// "Simple" Instruction

SimpleInstr {
  aff
  | postPreIncr
  | rep
  | FuncCall
  | kw<'return'> expr?
  | inc
  | VarDecl
  | kw<'break'>
  | kw<'continue'>
  | () // NOP
}

//// If

IfBlock { kw<'if'> ParenExpr instr (!right kw<'else'> instr)? }

//// Loops

loop { ForLoop | WhileLoop | RepeatLoop | ForeachLoop }

ForLoop { kw<'for'> ForExpr instr }
ForExpr { '(' AffFunc ';' expr ';' AffFunc ')' }

WhileLoop { kw<'while'> ParenExpr instr }

RepeatLoop { kw<'repeat'> instr kw<'until'> expr ';' }

ForeachLoop { kw<'foreach'> Identifier ParenExpr instr }

AffFunc { aff | postPreIncr | FuncCall | () /* NOP */ }

//// Repitition

rep { FuncCall ckw<'x'> expr }

//// Include

inc { kw<'include'> IncExpr }
IncExpr { '(' String ')' }

//// Function Call

FuncCall { Identifier ArgList }

ArgList { '(' list<arg> ')' }

arg { expr | NamedArg }

NamedArg { Identifier ':' Expr }

//// Affectation

aff { lvalue (AssignOp | UpdateOp) expr }

Aff { aff }

////

lvalue { Identifier | ArrayElem }

Identifier { identifier }

ArrayElem { Identifier '[' expr ']' }

postPreIncr { '++' lvalue | '--' lvalue | lvalue '++' | lvalue '--' }

//// Expression

expr {
  ParenExpr
  | expr !right LogicOp expr
  | '!' expr
  | expr !right ArithOp expr
  | '-' expr
  | '~' expr
  | expr !right BitOp expr
  | postPreIncr
  // match nomatch re_match re_nomatch ...
  | expr !right CmpOp expr
  | var
  | Aff
  | IpAddr
  | atom
  | ConstArray
}

Expr { expr }

ParenExpr { '(' expr ')' }

////

ConstArray { '[' list<arrayData> ']' }

arrayData { atom | String '=>' atom }

atom { Integer | String }

var { Identifier | ArrayElem | FuncCall }

IpAddr {
  Integer '.' Integer '.' Integer '.' Integer
}

//// Local/Global var decl

VarDecl { (kw<'local_var'> | kw<'global_var'>) list<Identifier> }

//// Operators

ArithOp { '+' | '-' | '*' | '**' | '/' | '%'}

AssignOp { '=' }

BitOp { '&' | '^' | '|' | '>>' | '>>>' | '<<' }

CmpOp { '><' | '>!<' | '=~' | '!~' | '<' | '>' | '==' | '!=' | '>=' | '<=' }

LogicOp { '&&' | '||' }

UpdateOp { '+=' | '-=' | '*=' | '/=' | '%=' | '>>=' | '>>>=' | '<<=' }

////

@skip { whitespace | Comment }

// Keyword
kw<term> { @specialize[@name={term}]<identifier, term> }
// Contextual keyword
ckw<term> { @extend[@name={term}]<identifier, term> }

@tokens {
  whitespace {
    ($[ \t\r\n] | '\r'? '\n')+
  }

  identifier {
    $[A-Za-z_] $[A-Za-z0-9_]*
  }

  Integer {
    $[0-9]+
    | "0" $[xX] $[0-9a-fA-F]+
  }

  xChar { $[0-9a-fA-F\n] }

  char {
    '\\' $[\\nftrv"0'\n]
    | '\\x' xChar xChar
    | !['\\]
  }

  String {
    "'" char* "'"
    | '"' !["]* '"'
  }

  Comment[isolate] { '#' ![\n\r]* }

  // literal tokens
  ';' '{' '}' '(' ')' '[' ']' '=>' ':' '++' '--' '!' '~'
}

@external propSource highlighting from './highlight'
